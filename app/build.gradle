apply plugin: 'com.android.application'
apply plugin: 'com.palantir.git-version'
import com.android.build.OutputFile

//TODO:need version only for release, literal for debug.
ext {
    git = gitVersion()
    gitSplit = git.tokenize('.')
    gitVersionCode = gitSplit.get(2).tokenize('-').get(0).toInteger()
    gitVersionName = "${gitSplit.get(0)}.${gitSplit.get(1)}"
}
def userHome = System.getProperty('user.home')
def keyStore = new Properties()
def baseFileName = "CertifySnap"
keyStore.load(new FileInputStream("$userHome/certifysnap/keystore.properties"))
android {

    signingConfigs {
        Snap {
            storeFile new File("$userHome/certifysnap/CertifySnap.jks")
            storePassword = keyStore['snap.passwd']
            keyAlias = 'certifysnap'
            keyPassword = keyStore['snap.passwd']
        }
        SnapEnt {
            storeFile file("$userHome/certifysnap/CertifySnapEnt.jks")
            storePassword = keyStore['snapEnt.passwd']
            keyAlias = 'snapent'
            keyPassword = keyStore['snapEnt.passwd']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.certify.snap"
        minSdkVersion 23
        targetSdkVersion 28
        compileSdkVersion 28
        versionCode = gitVersionCode
        versionName = gitVersionName
        versionNameSuffix ""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        testOptions.unitTests.includeAndroidResources = true
        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    flavorDimensions "version"
    productFlavors {
        Snap {
            dimension = 'version'
            signingConfig signingConfigs.Snap
        }
        SnapEnt {
            dimension = 'version'
            applicationIdSuffix ".ent"
            signingConfig signingConfigs.SnapEnt
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ENDPOINT_URL", "\"https://api.certify.me\""
        }
        qa {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ENDPOINT_URL", "\"https://apiqa.certify.me\""
        }
        debug {
            minifyEnabled false
            buildConfigField "String", "ENDPOINT_URL", "\"https://apidev.certify.me\""
        }
    }
    testOptions {
        unitTests.all {
            systemProperty 'robolectric.enabledSdks', '25'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}
android.applicationVariants.all { variant ->
    def buildType = variant.buildType.buildType.name
    variant.outputs.all { output ->

        def suffix = output.apkData.fullName == "snapEntRelease" ? "-ent" : ""
        version = buildType == "release" ? "${output.apkData.versionName}.${output.versionCode}" : "${'git rev-parse --short HEAD'.execute().text.replace('\n', '')}";
        output.outputFileName = "$baseFileName$suffix-${buildType}-${version}.apk"
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'org.litepal.android:java:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.tamic.novate:novate:1.5.5'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    //  api project(path: ':myokhttp')
    implementation files('libs/core-3.1.0.jar')
    implementation files('libs/telpo_api.jar')
    implementation files('libs/util.jar')
    implementation files('libs/arcsoft_image_util.jar')
    implementation files('libs/arcsoft_face.jar')
    implementation files('libs/pushservice-6.5.0.75.jar')
    implementation files('libs/telpo_temperature_v1.0.5.jar')
//    implementation files('libs/poi-3.12-android-a.jar')
//    implementation files('libs/poi-ooxml-schemas-3.12-20150511-a.jar')
    def appCenterSdkVersion = '3.2.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'

    implementation 'com.google.mlkit:barcode-scanning:16.0.0'
    implementation 'com.google.firebase:firebase-iid:20.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.0'

    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation 'me.grantland:autofittextview:0.2.1'

    testImplementation 'commons-logging:commons-logging:1.2'
    testImplementation "org.mockito:mockito-core:3.3.3"
    androidTestImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation "org.robolectric:robolectric:4.3.1"

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "androidx") {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "${targetSdkVersion}.+"
                }
            }
        }}
}
apply plugin: 'com.google.gms.google-services'

